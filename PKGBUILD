# $Id: PKGBUILD 208648 2014-03-24 19:45:08Z pierre $
# Maintainer: Pierre Schmitz <pierre@archlinux.de>

pkgname=ca-certificates-no-cnnic
_pkgname=ca-certificates
pkgver=20140325
pkgrel=1
pkgdesc='Common CA certificates - Remove Suspect CNNIC ROOT CA'
arch=('any')
url='http://packages.qa.debian.org/c/ca-certificates.html'
license=('MPL' 'GPL')
source=("http://ftp.debian.org/debian/pool/main/c/${_pkgname}/${_pkgname}_${pkgver}.tar.xz"
        'remove-cnnic.patch')
depends=('bash' 'run-parts' 'openssl' 'findutils' 'coreutils' 'sed')
makedepends=('python2')
provides=("$_pkgname=$pkgver")
conflicts=("$_pkgname")
install='ca-certificates.install'
backup=('etc/ca-certificates.conf')
sha256sums=('c0e3d8c517995db2737f7f1a9b69d654b8823fa6d337871c6ce111fcf083454a'
            'c10cdbf2d51bc53d8501b3c61c4a97c2ecef527dc7a70a8340c89ff4ad661184')

prepare() {
	cd ${srcdir}/${_pkgname}
	sed 's|/usr/bin/python|/usr/bin/python2|g' -i mozilla/certdata2pem.py
	sed 's|python|python2|g' -i mozilla/Makefile

    # Remove CNNIC CA
    cd mozilla
    patch -p5 -i "$srcdir/remove-cnnic.patch"
}

build() {
	cd ${srcdir}/${_pkgname}
	make SUBDIRS=mozilla
}

package() {
	cd ${srcdir}/${_pkgname}
	install -d -m755 ${pkgdir}/{etc/ca-certificates/update.d,usr/{sbin,share/ca-certificates},etc/ssl/certs}
	make install SUBDIRS=mozilla DESTDIR=${pkgdir}
	mv ${pkgdir}/usr/sbin ${pkgdir}/usr/bin
	install -D -m644 sbin/update-ca-certificates.8 ${pkgdir}/usr/share/man/man8/update-ca-certificates.8

	(
	echo "# Automatically generated by ${_pkgname}-${pkgver}-${pkgrel}"
	echo "# see update-ca-certificates man page"
	echo "# "
	cd ${pkgdir}/usr/share/ca-certificates
	find . -name '*.crt' | sort | cut -b3-
	) > ${pkgdir}/etc/ca-certificates.conf
}
